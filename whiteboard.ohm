WhiteBoard  {

  Program     =  Statement*

  Statement   =  If                                         --if
              |  For                                        --for
              |  While                                      --while
              |  Return                                     --ret
              |  Decl                                       --decl
              |  Break                                      --break
              |  Exp                                        --exp


  Decl        =  ObjDecl                                    --obj
              |  FunDecl                                    --fun
              |  id ("=" Exp)                               --var
              |  type id ("=" Exp)?                         --init

  ObjDecl     =  "Type" type "=" Param ":" Block
  FunDecl     =  type? id "=" Param ":" Block

  Binding     =  Exp ":" Exp

  Return      =  "return" Exp
  Break       =  "break"

  For         =  "for" id "in" Exp ":" Block

  If          =  "if" Exp ":" Block
                 ("else" "if" Exp ":" Block)*
                 ("else" ":" Block)?

  While       =  "while" Exp ":" Block

  Block       =  indent Statement* dedent

  Exp         =  And

  Param       =  "(" (SParam ("," SParam)*)?  ")"
  SParam      =  Binding                                     --Binding
              |  type? id                                    --id


  Args        =  "(" (Exp ("," Exp)*)?  ")"                 --exp
              |  "(" (Binding ("," Binding)*)?  ")"         --named

  Access      =  "." id                                     --lit
              |  "[" Exp "]"                                --exp

  And         =  And "and" Or                              --bin
              |  Or
  Or          =  Or "or" Rel                               --bin
              |  Rel
  Rel         =  Term relop Term                           --bin
              |  Term

  Term        =  Term addop Fact                           --bin
              |  Fact
  Fact        =  Fact mulop Neg                            --bin
              |  Neg

  Neg         =  negop Power                               --neg
              |  Power

  Power       =  Power powop Exp2                          --bin
              |  Exp2

  Exp2        =  Exp2 Args                                 --call
              |  Exp2 Access                               --acc
              |  Primary                                   --prim

  Primary     =  numlit                                    --num
              |  boolit                                    --bool
              |  strlit                                    --str
              |  id                                        --id
              |  "(" Exp ")"                               --exp


  type        =  ~keyword upper(idrest)*

  numlit      =  digit+ ("." digit+)?                      --whole
              |  "." digit+                                --dec
  boolit      =  "true" | "false"
  strlit      =  "'" ( ~"'" any )* "'"


  relop       =  ">=" | ">" | "<=" | "<" | "!=" | "=="

  addop       =  "+" | "-"
  mulop       =  "*" | "/" | "%" | "mod"

  negop       =  "-" | "!" | "not"
  powop       =  "**"
  indent      =  "⇨"
  dedent      =  "⇦"

  newline     =  "\n"+
  space      :=  " " | "\t" | "\n"

  keyword     =  ( "not" | "true" | "false" | "Type" | "fun" | "for" | "in" | "return" | "break" | "or" | "and" | "if" | "else" | "mod" | "return") ~idrest


  id          =  ~keyword(letter)idrest*
  idrest      =  letter | digit | "_"

}
